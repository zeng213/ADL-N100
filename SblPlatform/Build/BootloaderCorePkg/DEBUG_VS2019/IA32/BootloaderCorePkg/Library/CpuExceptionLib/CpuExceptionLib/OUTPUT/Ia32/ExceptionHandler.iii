;------------------------------------------------------------------------------ ;
; Copyright (c) 2017, Intel Corporation. All rights reserved.<BR>
; SPDX-License-Identifier: BSD-2-Clause-Patent
;
; Module Name:
;
;   ExceptionHandler.Asm
;
; Abstract:
;
;   IA32 CPU Exception Handler
;
; Notes:
;
;------------------------------------------------------------------------------

SECTION .text

extern _CommonExceptionHandler

;
; exception handler stub table
;
ALIGN   4
AsmIdtVectorBegin:
%rep  32
    db      0x6a        ; push  #VectorNum
    db      ($ - AsmIdtVectorBegin) / ((AsmIdtVectorEnd - AsmIdtVectorBegin) / 32) ; VectorNum
    jmp     _CommonInterruptEntry
%endrep
AsmIdtVectorEnd:

;----------------------------------------------------------------------------;
; CommonInterruptEntry                                                       ;
;----------------------------------------------------------------------------;
; The follow algorithm is used for the common interrupt routine.
; Stack:
; +---------------------+
; +    EFlags           +
; +---------------------+
; +    CS               +
; +---------------------+
; +    EIP              +
; +---------------------+
; +    Error Code       +
; +---------------------+
; +    Vector Number    +
; +---------------------+
global _CommonInterruptEntry
_CommonInterruptEntry:
    cli
    push    esp
    call    _CommonExceptionHandler
    add     esp, 8
    jmp     $

;----------------------------------------------------------------------------;
; _AsmGetTemplateAddressMap                                                  ;
;----------------------------------------------------------------------------;
;
; void
; AsmGetTemplateAddressMap (
;     EXCEPTION_HANDLER_TEMPLATE_MAP *AddressMap
;   );
;
; Routine Description:
;
;  Return address map of interrupt handler template so that C code can generate
;  interrupt table.
;
; Arguments:
;  AddressMap:  Address of EXCEPTION_HANDLER_TEMPLATE_MAP structure
;
; Returns:
;   Nothing
;-----------------------------------------------------------------------------;
global _AsmGetTemplateAddressMap
_AsmGetTemplateAddressMap:
    push    ebp
    mov     ebp, esp
    mov     eax, dword [ebp + 0x8]
    mov     dword [eax], AsmIdtVectorBegin
    mov     dword [eax + 4], (AsmIdtVectorEnd - AsmIdtVectorBegin) / 32
    pop     ebp
    ret
